#!/bin/zsh

# Load our prompt settings.

# Standard colors
dark_grey="$(print '%{\e[38;5;239m%}')"
med_grey="$(print '%{\e[38;5;243m%}')"
light_grey="$(print '%{\e[38;5;252m%}')"
green_1="$(print '%{\e[38;5;190m%}')"
green_2="$(print '%{\e[38;5;118m%}')"
orange_1="$(print '%{\e[38;5;202m%}')"
orange_2="$(print '%{\e[38;5;214m%}')"
yellow="$(print '%{\e[38;5;227m%}')"

git_info() {
    git_is_git || return

    scm="$dark_grey(git::$med_grey$(git_branch)$dark_grey:$med_grey$(revision $(git_head))"

    local dirty

    if ! git_is_index_clean; then
	dirty+="+"
    fi

    if ! git_is_work_tree_clean; then
	dirty+="!"
    fi

    if git_has_unmerged; then
	dirty+="*"
    fi

    if git_has_untracked; then
	dirty+="$dark_grey?"
    fi

    if [ $#dirty -gt 0 ]; then
	scm+="$dark_grey | $med_grey$dirty "
    fi

    scm+="$dark_grey)"

    echo $scm
}

revision() {
    git describe --always $1 2>/dev/null ||
    git rev-parse --short $1 2>/dev/null
}

# Fancy prompt.
smart_prompt() {

    # Prompt data --------------------------------------------------------------

    user=${$(whoami):l}              # The current user, lowercase

    # Left hand prompt ---------------------------------------------------------

    l_prompt='
$med_grey$user$'                     # User name (preceded by newline)
    l_prompt+='dark_grey@'           # @ separator
    l_prompt+='$med_grey%m '         # Machine name
    l_prompt+='$dark_grey:: '        # :: separator
    l_prompt+='$light_grey%~ '       # Current path
    l_prompt+='$(git_info)'          # Git information if relevant
    l_prompt+='
$green_1%m '                         # Machine name (preceded by newline)
    l_prompt+='$light_grey%(!.#.>) ' # Prompt (varies if root)
    l_prompt+='$light_grey'          # Reset color to light grey
    
    # Right hand prompt --------------------------------------------------------

    r_prompt='$med_grey%h '          # History line
    r_prompt+='$dark_grey| '         # | separator
    r_prompt+='$med_grey%* '         # Time
    r_prompt+='%D{%f/%m}'            # Date
    r_prompt+='$light_grey'          # Reset color to light grey

    # Set prompts --------------------------------------------------------------

    PROMPT=$l_prompt                 # Set the main (left) prompt
    RPROMPT=$r_prompt                # Set the right prompt
}

# Basic prompt.
dumb_prompt() {
    PS1='$ '
    unsetopt zle
    unsetopt prompt_cr
    unsetopt prompt_subst
    unfunction precmd
    unfunction preexec
}

# Set prompts.
if [[ $TERM == "dumb" ]]; then
    dumb_prompt # Use a super simple prompt for tramp, etc.
else 
    smart_prompt # Use a nice fancy prompt in normal shells.
fi